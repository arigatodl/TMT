!;монгол хэлний үгийн задлуур

;ALPHABET
;  а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я - + Б Ү Д А Э И Ө У Ч Х Й Ж Е Н Г Ы
;NULL 0
;ANY  @
;BOUNDARY #
SUBSET C б в г д ж з к л м н п р с т ф х ц ч ш щ е ё ю я
SUBSET V а и о ө у ү э 
SUBSET GG э ү ө 
SUBSET RR а о у я
SUBSET MN м н г л б в р                         
SUBSET ZZ д ж з ц с т х ч ш 
SUBSET ZH д ж з ц с т ч ш
SUBSET ZS д ж з ц т ч ш х
SUBSET ZD ж з ц т ч ш 
SUBSET ZT д з ц с т х                   
SUBSET TT ч ш ж к
SUBSET DP г с р в
SUBSET SS ч ш ж г 				
SUBSET QL б в д з л м н п р с т ф х ц е ё ю я 
SUBSET FK ю я е ё
SUBSET WW м н л в
SUBSET OO а у я
SUBSET FF э ү и е ю
SUBSET YY а и о ө у ү э й 
SUBSET BB э ү е ю ө
SUBSET II д ж з ц с т ч ш 
SUBSET JJ г н
SUBSET PP с т м р 
SUBSET MM м н г л б в 
SUBSET HH д л
SUBSET FI э ү 
SUBSET OI а и
SUBSET BI а и о ө у ү э ь
SUBSET DS д с



PAIRS  б в г д ж з к л м н п р с т ф х ц ч ш щ ъ ы ь ю я е ё 
       б в г д ж з к л м н п р с т ф х ц ч ш щ ъ ы ь ю я е ё  

PAIRS  а и о ө у ү э - +
       а и о ө у ү э - 0

PAIRS Б Ү Д А Э И Ө У Ч Х Й Ж Е Н Г Ы 
      Б Ү Д А Э И Ө У Ч Х Й Ж Е Н Г Ы 

RULE Д:д /<= [г|с ] +:0 ___
!; Ын:ны хувиргах дүрэм
RULE +:н => [ээ|аа|оо|өө|уу|үү] ___ [Д:|Ы: (Ы:@) н: (н:@) ]
RULE н:ы => [аа|оо|уу] +:@ Ы: (Ы:@) ___
RULE Ы:0 /<= [ээ|өө|үү] +:0 ___ н: (н:@)
RULE Ы:0 => [аа|оо|уу|ээ|өө|үү|ий|ай|ой|уй|эй|үй] +:@ ___ 

!; Ыг:ийг- хувиргах д үрэм
RULE Ы:й <=> [[GG* C* |RR* C* TT|г |и C*|C* и: (и:@) QL] +:и|C* и +:0|ь: (+:0)|[GG|GG C*] +:0 0: (0:@) н: (н:@)] ___ 
RULE +:и /<= [[C|C*] RR [RR*|й*] QL*|C* и|V|й|RR RR|RR C* и QL*| C RR C* и:(и:0) QL*|RR TT и х] ___   
RULE +:и => [GG* C* | RR* C* TT|г |и C*] ___ Ы: 
RULE Ы:ы /<= [GG | GG C*| RR* C* TT| г|аа|оо|уу] +:0 ___ см   
RULE Ы:й /<= [C* [RR|я] [RR*|й*] QL|й|RR RR|C* RR:(RR:@) QL|C RR C* и:(и:0) QL|RR TT с RR н ] +:@ ___  

 !; нЫ:ний- хувиргах дүрэм
RULE Ы:и <=> [ээ|өө|үү] +:@ ___ н: (н:@)
RULE н:й <=> [ээ|өө|үү] +:@ Ы: (Ы:@) ___ 
RULE Ы:и /<= [RR| RR й* C*| RR* и|C ь] +:0 ___ 
RULE Ы:ы => RR [C* и |й*|C*|C* и: (и:@)|C* RR:(RR:@) C*|ь:(ь:@)] QL* +:0 н* ___ 
RULE 0:н <=> [GG|GG C*] +:0 ___ н:@ Ы:@
RULE н:и <=> [GG|GG C*] +:0 0: (0:@) ___ Ы:@
!;RULE 0:и <=> [GG|[GG|и] C*] +:0 н ___ Ы:@

 !; Ын:ы н-ээр төгссөн үгийг хувиргах дүрэм
RULE н:0 <=> [V: (V:@) н|JJ [{а,о}: ({а,о}:@)|{ө,э}: ({ө,э}:@)]|MN с а: (а:@) н] +:@ Ы: (Ы:@) ___
!; Хэлний угийн н:нг болгох дүрэм
!;RULE 0:г <=> V н ___ +:@ C* [Ы:@|Ү:@|У:@]
!; эгшгээр төгссөн үгэнд Ын:ын болгох дүрэм
RULE {а,о}:ы <=> JJ ___ +:0 Ы: (Ы:@)
RULE Ы:н <=> JJ [{а,о}: ({а,о}:@)|{ө,э}: ({ө,э}:@)] +:@ ___ н:
RULE Ы:г <=> JJ [{а,о}: ({а,о}:@)|{ө,э}: ({ө,э}:@)] +:@ ___ г:
RULE г:0 <=> JJ [{а,о}: ({а,о}:@)|{ө,э}: ({ө,э}:@)] +:@ Ы: (Ы:@) ___
!; эгшгээр төгссөн үгэнд Ын:ийн болгох дүрэм
RULE {ө,э}:и <=> JJ ___ +:@ Ы: (Ы:@)
RULE +:й => JJ {ө,э}: ({ө,э}:@) ___ Ы: (Ы:@)
RULE +:й /<= JJ [ээ|өө|үү] ___ Ы: (Ы:@)
!;эгшиг зохицох ёс
!;RULE Ү:а => [ OO [C*|ь:(ь:@)|а:(а:@) C*|й C*|[aй|BB н] 0: (0:@)]|ы|й|ай C* а:(а:@) C*] +:@ C* (Б: (Б:@)) (Ү: (Ү:@)) C* ___ 
!; mon.rul deer ni "0: (0:@)|ь: (ь:@)" а:(а:@) C*-iin urd nemne. harin egshig oruulsan bol 
!; +:@ ;daraa (0: (0:@)) nemeh heregtei.

 
RULE Ү:э => FF [и* C*|й C*|ь: (ь:@)|э: (э:@) C*] +:@ C* и* (Б: (Б:@)) (Ү: (Ү:@)) C* ___ 
RULE Ү:о => о [и* C*|й C*|ь: (ь:@)|о: (о:@) C*] +:@ C* и* (Б: (Б:@)) (Ү: (Ү:@)) C* ___ 
RULE Ү:ө => ө [и* C*|й C*|ь: (ь:@)|ө: (ө:@) C*] +:@ C* и* (Б: (Б:@)) (Ү: (Ү:@)) C* ___ 
RULE Ү:а => OO [и* C*|й C*|ь: (ь:@)|OI:(OI:@) C*] +:@ C* и* (Б: (Б:@)) (Ү: (Ү:@)) C* ___ 
!;баганын тоо 12-аас хэтэрсэн тул үүнийг дахин ажлуулна.нэг дүрмэнд багтах боломжгүй
RULE +:х => [ы|ий|Ы] н* ___ Ү:@
!;RULE Ү:э /<= OO C*[#|и C*] +:@ C* (Б: (Б:@)) (Ү: (Ү:@)) C*  ___
!;гээгдэх эгшгийн дүрэм 
RULE а:0 => [C* [BI|FK] й* [ZZ|MN DS*|с т]|ь д] ___ [[ZH|MN] +:@ [Ы:ы|Ү:@|У:@] | SS +:@ [Ы:@|Ү:@|У:@]] 
RULE о:0 => [C* BI й* [ZZ|MN]|ц|MN DS] ___ [[ZH|MN] +:@ [н#|г|Ы:ы| Ү:@|У:@|х л]| SS +:@ [Ы:|Ү:@|У:@|0:@] ]
RULE {а,о}:0 /<= C* RR JJ ___ C +:@ [Ы:|У:|Ү:]   
RULE и:0 <=> [V MN* TT | ZZ ZZ] ___ [MN | д] +:@ [Ы:|Ү:|У:]  
RULE и:0 /<= V д ч ___ н +:@ Ы:
RULE э:0 => [V [MN|ZZ]|MN ZZ] ___ [MN|ZZ] +:@ [х л|MN C|MN#|Ү:@|Ы:@|У:@]
!;үйлт нэрийн х-ийн өмнөх эгшиг гээхгүй. харин л байсаныг хасав учрыг олох
RULE {э,ө}:0 /<= [V|FK] [MN|ZT] ___ х +:@ [Ы:|У:|Ү:]    
!;RULE {э,ө}:0 /<= [V C* ___ х|GG н ___ л] +:@ [Ы:|У:|Ү:] 
!;RULE э:0 /<= GG [д|т]  ___ с +:@  (0: (0:@))
RULE ө:0 => [V [ MN|ZZ]|[MN|х|ө] ZZ|GG х] ___ [MN|ZH] +:@ [Ы:@|MN C|Ү:@|У:@]

!;Зөөлний тэмдэгийн дүрэм
RULE ь:и => C* ___ +:0 [Б:|н#|н (г) [Ы:ы|Д:|Ү:|У:]|Ы:|MN:|ZZ:|Ж:|Ү:|У:] 
RULE ь:и /<= MN ___ +:0 [II|FK|Ж:]
!;Ө\о нөхцлөөр хувиргах дүрэм
RULE Д:т => DP +:0 ___
RULE +:a => [OO C* и* MN* ZT|RR C* д|с а C* и х] ___ [MN MN|MN#|ZZ#|ZZ MN|Д:|Ж:@ #|х л]
RULE +:э => [FI C* [ZT|MN]|э: (э:@) MN|MN х|FI HH|FI MN] ___ [MN#|MN C|ZZ#|Д:|х л]
!;RULE +:{о,ө} => [{о,ө} C* [ZT|MN|и* х]|{о,ө}: ({о,ө}:@) C|{о,ө} MN* MN и х] ___ [н Д:|н#|MN т|MN#|MN C|ZZ|Д:|Ж:@ #]
RULE +:{о,ө} => [{о,ө} C* [ZT|MN|и* х]|{о,ө}: ({о,ө}:@) C|{о,ө} MN з] ___ [н Д:|н#|MN т|MN#|MN C|ZZ|Д:|Ж:@ #]
RULE 0:и <=> [(MN)|э: (э:@)] TT +:0 ___ [MN#|ZZ MN|Д:|MN C|г#|Ж:@ #|г:]
!;RULE 0:и /<= ө TT +:0 ___ Ж:@ э:
!; эгшиг жийрэглэхийг хориглох
RULE +:а /<= [RR RR с|TT|[OO|л] ZH|OO MN] ___ [Д:|ZZ [MN|OO|и|ZZ Ө:]|Ж:|Ж: э]
RULE +:э /<= [GG з|с|т|TT|г|р|л|в|м|RR C* и х|с э н] ___ [г#|ZZ Й:|MN Ү:|ZZ Ө:|ZZ Ү:|ZZ У:|Д:|Ж:|TT]
jinhene ni 
!;RULE +:{о,ө} /<= {о,ө} [ZD|TT|MN|[өө|оо] р] ___ [ZZ MN|MN#|ZZ и|ZZ Ү:|ZZ У:|ZZ Э:|Д:|Ж:|Ж: э]
!;RULE +:э /<=  [с|в|GG з|г|р|в|э м] ___ [ZZ У:|ZZ Ү:|Д:|ZZ У:|Ж:|ч:]
!;Урт уу,үү нөхцлүүд хувилбар
RULE {а,о}:у /<= [аа|оо|уу] ___ +:@
!;RULE У:ү => [[GG|и] [C*|й C*|ь: (ь:@)|GG: (GG:@) C*]|{э,ө}|C] +:@ (0: (0:@)) C* ___
+:@ daraa (0: (0:@)) nemeh yostoi.
RULE У:ү => [[GG|и] [C*|й C*|ь: (ь:@)|GG: (GG:@) C*]|{э,ө}|C] +:@ C* (У: (У:@)) ___
!;RULE У:у => RR [C*|й C*|ь:(ь:@)|RR:(RR:@) C*|{а,о}] +:@ C* (У:(У:@)) ___
RULE У:у => RR [и* C*|й C*|ь:(ь:@)|RR:(RR:@) C*] +:@ (0: (0:@)) C* (У:(У:@)) ___
RULE {а,о}:у => C* ___ +:@ У: (У:@)
 
RULE {э,ө}:ү <=> C* ___ +:@ У: (У:@)
!; урт эгшиг дан болох
RULE Ү:0 <=> [V|FK|ь: (ь:@)|{э,ө}: ({э,ө}:@)] +:0 ___
RULE У:0 <=> [V|FK|ь: (ь:@)|{а,о}: ({а,о}:@)] +:0 ___
RULE +:г => [ий|уй|эй|үй|ээ|аа|оо|өө|уу|үү|ай|ой] ___ [Ү:|У: ] 
!;нэрээс нэр үүсгэх "БҮр" дагавар3
RULE Б:б => WW +:0 ___ 
RULE Б:в /<= WW +:0 ___ 
!;хамтрахын нөхцлөөр хувиргах дүрэм
RULE Й:a => OO [C* и*|й C*] +:@ C* ___ 
RULE Й:э => [BB|и*] [C* и*|й C*] +:@ C* ___  
RULE Й:о <=> о [C* и*|й C*] +:@ C* ___
RULE Й:э /<= [RR C*|RR C* и C] +:@ C* ___
!;нэрээс нэр үүсгэх "мхӨй" дагавар3
RULE Ө:а => OO [C*|й C*] +:@ C* (Н: (Н:@)) C* ___  
RULE Ө:и => FF [C*|й C*] +:@ C* (Н: (Н:@)) C* ___   
RULE Ө:о => о [C*|й C*] +:@ C* (Н: (Н:@)) C* ___
!;нэрээс нэр үүсгэх "гИй"дагавар2 зөрчилтэй
RULE И:а <=> RR C +:0 (0: (0:@)) C ___
RULE И:и <=> GG C +:0 C ___
!;нэрээс нэр үүсгэх "дЭй", "CАC "дагавар2
RULE Э:{а,о} => {а,о} C +:0 C ___
!;нэрээс нэр үүсгэх "CАC "дагавар2 
RULE А:э => [GG|и] [GG*|C*|й C*|ь: (ь:@)|GG: (GG:@) C*] +:@ C* (У: (У:@)) C* ___
RULE А:а => RR [и*|RR*|C*|й C*|ь: (ь:@)] +:@ C* (У: (У:@)) C* ___
!; Хэлний угийн Н:нг болгох дүрэм
RULE Н:н <=> V ___ (0: (0:@)) +:@  C* [Ы:@|Ү:@|У:@]
RULE 0:г <=> V Н:@ ___ +:@ C* [Ы:@|Ү:@|У:@]

!;Үйлийн хүсэх нөхцөл
RULE Е:я => OO [C*|и|й|ь] +:@ ___ 
RULE Е:е => GG C* +:@ ___ 
RULE Е:ё => о [C*|и|ь|C* и C] +:@ ___ 
RULE +:ъ => [RR C*|RR C* и C] ___ Е:
RULE +:ь => C* GG C* ___ Е:
RULE +:ъ /<= RR ___ Е:
RULE +:ь /<= GG ___ Е:
!;Үйлийн зэрэгцүүлэн холбох нөхцөл
RULE Ж:ж => V* [ZZ|MN|YY|ь: (ь:@)|FK] +:@ (0: (0:@)) ___ 
RULE Ж:ч => C* V C* [V|й] [р|с]  +:0 ___
RULE Ж:ж /<= C* V C V [р|с]  +:0 (0: (0:@)) ___
!;Үйлийн х өмнө эгшиг авна
!;RULE 0:а <=> [OO MM|C а PP] +:0 ___ [х MN|н Д:|Ж:] 
RULE 0:а <=> [RR C* и д|C RR р|C а PP|OO MM|аа с] +:0 ___ [н Д:|Ж:@ #|р|х|[н|г] г|в|г {т,д}|г#|н#|г ч] 
RULE 0:о <=> [о|о C* и] MN +:0 ___ [х|г#|г т|[н|г] г|MN#|MN C]
RULE 0:ө <=> ө MN +:0 ___ [х|MN#]
RULE 0:э <=> [FF з|ү р х|ү т|н э д] +:0 ___ [н#|Ж:|х#|в#]
RULE 0:э /<= RR C и р +:0 ___ MN#
!;RULE 0:а /<= аа MN +:0 ___ MN [И|RR|Ү:] 
 

END