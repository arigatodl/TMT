; TURKISH.RUL  25-Oct-89

; Data from Problem Book in Phonology, by Halle & Clements, MIT Press,
; 1983, pp. 137-139.

; • = mid central unrounded
; š = mid front rounded
; Ÿ = high front rounded
; ’, ‡, œ = long vowels
; L = palatalized l
; $ = voiceless alveopalatal sibilant
; c = voiceless alveopalatal affricate
; j = voiced alveopalatal affricate

; archiphonemes:
; D = alveolar stop
; A = nonhigh unrounded
; H = high
; ” = front unrounded
; ž = front rounded
; » = back unrounded
; § = back rounded

ALPHABET
  p t k b d g m n f v s z $ c j l L r w y h D +
  a e i o u ‡ ’ œ • š Ÿ A H ” ž » ¼
NULL 0
ANY  @
BOUNDARY #
SUBSET C p t k b d g m n f v s z $ c j l L r w y h D  ; consonants
SUBSET V a e i o u ‡ ’ œ • š Ÿ A H ž » ” ¼            ; vowels
SUBSET V[-lng] a e i o u • š Ÿ  ; short vowels
SUBSET V[+lng] ‡ ’ œ            ; long vowels
SUBSET V[-bk-rd] i ’ e ”        ; front unrounded
SUBSET V[-bk+rd] Ÿ š ž          ; front rounded
SUBSET V[+bk+rd] u œ o ¼        ; back rounded
SUBSET V[+bk-rd] a ‡ • »        ; back unrounded
SUBSET V[+bk] a ‡ • u œ o » ¼   ; back
SUBSET V[-bk] i ’ e Ÿ š ž ”     ; front
SUBSET S s y                    ; used in S:0 rule
SUBSET O[-vc] p t k c           ; voiceless obstruents
SUBSET O[+vc] b d g j           ; voiced obstruents
SUBSET Q ” ž » ¼ A H            ; archiphonemes

RULE "1 Consonant defaults"  1 23
   p t k b d g m n f v s z $ c j l r w y h D + @
   p t k b d g m n f v s z $ c j l r w y h d 0 @
1: 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1

RULE "2 Vowel defaults"  1 9
   a e i o u • š Ÿ @
   a e i o u • š Ÿ @
1: 1 1 1 1 1 1 1 1 1

; C:@ due to S:0 rule
RULE "3 A:a <=> V[+bk]: C* +:0 C*__"  2 6
   V[+bk] C + A A @
   @      @ 0 a @ @
1: 2      1 1 0 1 1
2: 2      2 2 2 0 1

; C:@ due to S:0 rule
RULE "4 A:e <=> V[-bk]: C* (+:0) C*__"  2 6
   V[-bk] C + A A @
   @      @ 0 e @ @
1: 2      1 1 0 1 1
2: 2      2 2 2 0 1

; C:@ due to S:0 rule
; V[+bk+rd]:@ due to Q:0 rule
RULE "5 H:u <=> V[+bk+rd]: C* (+:0) __"  2 6
   V[+bk+rd] C + H H @
   @         @ 0 u @ @
1: 2         1 1 0 1 1
2: 2         2 2 2 0 1

; V[-bk+rd]:@ due to ž:0 rule
RULE "6 H:Ÿ <=> V[-bk+rd]: C* (+:0) __"  2 6
   V[-bk+rd] C + H H @
   @         @ 0 Ÿ @ @
1: 2         1 1 0 1 1
2: 2         2 2 2 0 1

; C:@ due to S:0 rule
RULE "7 H:i <=> [:V[-bk-rd] | V[-bk-rd]:] C* (+:0) __"  2 8
   @         V[-bk-rd] V[-bk-rd] C + H H @
   V[-bk-rd] @         V[-bk-rd] @ 0 i @ @
1: 2         2         2         1 1 0 1 1
2: 2         2         2         2 2 2 0 1

; C:@ due to S:0 rule
RULE "8 H:• <=> :V[+bk-rd] C* (+:0) __"  2 7
   A         V[+bk-rd] C + H H @
   V[+bk-rd] @         @ 0 • @ @
1: 2         2         1 1 0 1 1
2: 2         2         2 2 2 0 1

; Palatalization
; LR: dere+lAr  tilki
; SR: dere0Ler  tiLki

; no instance in data of this change occurring across morpheme boundary
RULE "Palatalization, l:L <= ___:e" 2 4
   l l @ @
   L @ e @
1: 1 2 1 1
2: 1 2 0 1

RULE "Palatalization, l:L <= V[-bk]:___" 2 4
   V[-bk] l l @
   @      L @ @
1: 2      1 1 1
2: 2      1 0 1

RULE "Palatalization, l:L => [V[-bk]:___ | ___:e]" 3 4
   V[-bk] l @ @
   @      L e @
1: 2      3 1 1
2: 2      1 1 1
3. 0      0 1 0

; Vowel Deletion
; LR: ka”lb+sH  usužl+sH  kira»+sH  huku¼k+sH
; SR: ka0Lb00i  usœ0L00Ÿ  kir‡00s•  hukœ0k00u

; include A and H?
; These vowels have no surface realization (except see ”:a rule);
; they condition vowel harmony.
RULE "Q:0" 1 5
   ” ž » ¼ @
   0 0 0 0 @
1: 1 1 1 1 1

; Lengthening
; LR: kira»
; SR: kir‡0

RULE "V[-lng]:V[+lng] correspondences" 1 4
   i a u @
   ’ ‡ œ @
1: 1 1 1 1

; V[-lng]:V[+lng] word-finally
RULE "Lengthening, V[-lng]:V[+lng] <= ___Q:0#" 3 5
   V[-lng] V[-lng] Q # @
   V[+lng] @       0 # @
1: 1       2       1 1 1
2: 1       2       3 1 1
3: 1       2       1 0 1

; LR: maAzi” maAzi”+sH
; SR: m‡0z’0 m‡0z’00si

; V[-lng]:V[+lng] in open syllable
RULE "Lengthening, V[-lng]:V[+lng] <= ___Q:0 C (+:0) C:0 V" 5 8
   V[-lng] V[-lng] Q + C C V @
   V[+lng] @       0 0 C 0 V @
1: 1       2       1 1 1 1 1 1
2: 1       2       3 2 1 1 1 1
3: 1       2       1 3 4 1 1 1
4: 0       0       1 4 1 5 0 1
5: 0       0       1 5 1 1 0 1

; V[-lng]:V[+lng] prohibited in word-final closed syllable
RULE "Lengthening, V[-lng]:V[+lng] /<= ___Q:0 C#" 4 5
   V[-lng] Q C # @
   V[+lng] 0 C # @
1: 2       1 1 1 1
2: 2       3 1 1 1
3: 2       1 4 1 1
4: 2       1 1 0 1

RULE "Lengthening, V[-lng]:V[+lng] => ___Q:0 [# | C (+:0) C:0 V]" 5 8
   V[-lng] Q # C + C V @
   V[+lng] 0 # C 0 0 V @
1: 2       1 1 1 1 1 1 1
2. 0       3 0 0 2 0 0 0
3. 0       0 1 4 3 0 0 0
4. 1       0 0 0 4 5 1 0
5. 0       0 0 0 5 0 1 0

; ”:a rule
; LR: sa”t  sa”t+sH
; SR: saat  saat00i

RULE "”:a <= #CV___C[+|#]" 6 7
   # C V ” ” + @
   # C V a @ 0 @
1: 2 1 1 1 1 1 1
2: 2 3 1 1 1 1 1
3: 2 1 4 4 4 1 1
4: 2 1 1 1 5 1 1
5: 2 6 1 1 1 1 1
6: 0 1 1 1 1 0 1

RULE "”:a => #CV___C[+|#]" 6 6
   # C V ” + @
   # C V a 0 @
1: 2 1 1 0 1 1
2: 2 3 1 0 1 1
3: 2 1 4 0 1 1
4: 2 1 1 5 1 1
5. 0 6 0 0 0 0
6. 1 0 0 0 1 0

; Consonant Deletion
; LR: at+sH  at+yA
; SR: at00•  at00a

RULE "S:0 correspondences" 1 3
   s y @
   0 0 @
1: 1 1 1

; S = s y
; C:@ due to k:0 rule
RULE "Deletion, S:0 <=> C (+:0)___" 2 5
   C + S S @
   @ 0 0 @ @
1: 2 1 0 2 1
2: 2 2 1 0 1

; D:t rule
; LR: kep+DAn
; SR: kep0ten

RULE "D:t <=> :O[-vc] (+:0)___" 2 5
   @      + D D @
   O[-vc] 0 t @ @
1: 2      1 0 1 1
2: 2      2 1 0 1

; Devoicing
; LR: kab  kab+DAn
; SR: kap  kap0tan

RULE "O[+vc]:O[-vc] correspondences" 1 5
   b d g j @
   p t k c @
1: 1 1 1 1 1

RULE "Devoicing, O[+vc]:O[-vc] <= ___#" 2 4
   O[+vc] O[+vc] # @
   O[-vc] @      # @
1: 1      2      1 1
2: 1      1      0 1

RULE "Devoicing, O[+vc]:O[-vc] <= ___(+:0)C" 2 5
   O[+vc] O[+vc] + C @
   O[-vc] @      0 C @
1: 1      2      1 1 1
2: 1      2      2 0 1

RULE "Devoicing, O[+vc]:O[-vc] => [___# | ___(+:0)C]" 2 5
   O[+vc] # + C @
   O[-vc] # 0 C @
1: 2      1 1 1 1
2. 0      1 2 1 0

; k-deletion
; LR: inek+sH
; SR: ine000i

; S = s y
RULE "k-deletion, k:0 <=> V[-lng]___(+:0)S:" 4 6
   V[-lng] k k + S @
   @       0 @ 0 @ @
1: 2       0 1 1 1 1
2: 2       4 3 2 1 1
3: 2       0 1 3 0 1
4. 0       0 0 4 1 0

END
